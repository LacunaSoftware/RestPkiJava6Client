apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.lacunasoftware.restpki'
archivesBaseName = 'restpki-client-java6'
version = '1.9.0'

repositories {
   mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    compile 'io.swagger:swagger-annotations:1.5.3'
}

task fatJar(type: Jar) {
    manifest {
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'restpki-client-java6'
                packaging 'jar'
                artifactId 'restpki-client-java6'
                description 'Classes to consume Lacuna Software REST PKI'
                url 'https://github.com/LacunaSoftware/RestPkiJava6Client'

                scm {
                    connection 'scm:git:git@github.com:LacunaSoftware/RestPkiJava6Client.git'
                    developerConnection 'scm:git:git@github.com:LacunaSoftware/RestPkiJava6Client.git'
                    url 'https://github.com/LacunaSoftware/RestPkiJava6Client'
                }

                licenses {
                    license {
                        name 'Unlicense'
                    }
                }

                developers {
                    developer {
                        id 'ismaelm'
                        name 'Ismael Medeiros'
                        email 'IsmaelM@lacunasoftware.com'
                    }
                }
            }
        }
    }
}
